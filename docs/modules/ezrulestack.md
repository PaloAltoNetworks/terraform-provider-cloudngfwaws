---
page_title: "cloudngfwaws: ezrulestack Module"
subcategory: ""
description: |-
  Module for rulestack manipulation.
---

# ezrulestack

Module for rulestack manipulation. 

ezrulestack module allows for faster and simpler workflow to create rulestacks, by defining the dependencies 
between security objects, rules, rulestack and commit resources implicitly. User can specify the resources
and ezrulestack module will resolve the dependencies and create the resources in the necessary order.

By default, ezrulestack module commit variable is set to `true`. It can be overridden if user
intends to keep the changes uncommitted.

Commit happens in two stages. First time `terraform apply` is run, terraform detects the changes in rulestack plan file
and provisions the changes to CloudNGFW. Second time the `terraform apply` is run, it detects the uncommitted changes
in CloudNGFW and initiates commit, provided the commit variable is set to `true` as mentioned above.


## Admin Permission Type

* `Rulestack` (for `scope="Local"`)
* `Global Rulestack` (for `scope="Global"`)


## Example Usage

```terraform
module "example-rulestack" {
  source                = "./modules/ezrulestack"
  rulestack             = var.my_rulestack
  security_rules        = var.my_security_rules
  feeds                 = var.my_feeds
  prefix_lists          = var.my_prefix_lists
  fqdn_lists            = var.my_fqdn_lists
  custom_url_categories = var.my_custom_url_categories
  predefined_url_category_overrides = var.my_predefined_url_category_overrides
  certificates          = var.my_certificates
  commit                = var.commit
}
```

where variables are initialized as

```
my_rulestack = {
  name        = "ezrulestack1"
  scope       = "Local"
  account_id  = 123456789
  description = "Made by Terraform"
  profile_config = {
    anti_spyware = "None"
  }
  lookup_x_forwarded_for = "SecurityPolicy"
}

my_security_rules = [
  {
    name        = "tf-security-rule1"
    rule_list   = "LocalRule"
    priority    = "1"
    description = "Also configured by Terraform"
    source = {
      cidrs       = ["any"]
      feeds       = ["feed1"]
      prefix_list = ["prefix-list1"]
    }
    negate_source = false
    destination = {
      prefix_list = ["prefix-list2"]
    }
    category = {
      url_category_names = []
    }
    applications  = ["any"]
    protocol      = "any"
    action        = "Allow"
    logging       = true
    audit_comment = "initial config"
  },
  {
    name      = "tf-security-rule2"
    rule_list = "LocalRule"
    priority  = "2"
    enabled   = false
    source = {
      feeds = ["feed2"]
    }
    destination = {
      cidrs     = ["192.168.2.0/24"]
      fqdn_list = ["fqdn1"]
    }
    negate_destination = false
    category = {
      url_category_names = ["tf-custom-category"]
    }
    applications   = ["any"]
    prot_port_list = ["TCP:90", "TCP:93", "UDP:100"]
    action         = "Allow"
    logging        = false
  }
]

my_feeds = [
  {
    name        = "feed1"
    description = "Also configured by Terraform"
    url         = "https://foobar.net"
    type        = "IP_LIST"
    frequency   = "DAILY"
    time        = 10
  },
  {
    name        = "feed2"
    description = "Also configured by Terraform"
    url         = "https://foobar2.net"
    type        = "IP_LIST"
    frequency   = "HOURLY"
    time        = 0
  }
]

my_prefix_lists = [
  {
    name        = "prefix-list1"
    description = "Also configured by Terraform"
    prefix_list = [
      "192.168.0.0",
      "10.1.5.0",
    ]
    audit_comment = "initial config"
  },
  {
    name          = "prefix-list2"
    description   = "Also configured by Terraform"
    prefix_list   = ["10.0.1.0/24"]
    audit_comment = "initial config"
  }
]

my_fqdn_lists = [
  {
    name        = "fqdn1"
    description = "Also configured by Terraform"
    fqdn_list = [
      "example1.com",
      "foobar1.org",
    ]
    audit_comment = "initial config"
  },
  {
    name        = "fqdn2"
    description = "Also configured by Terraform"
    fqdn_list = [
      "example2.com",
      "foobar2.org",
    ]
    audit_comment = "initial config"
  }
]

my_custom_url_categories = [
  {
    name        = "tf-custom-category"
    description = "Configured by Terraform"
    url_list = [
      "example.com",
      "foobar.org",
    ]
    # action = "alert" 
  }
]

my_certificates = [
  {
    name          = "tf-cert"
    description   = "Configured by Terraform"
    signer_arn    = "arn:aws:secretsmanager:us-east-1:123456789:secret:tf-test-inbound-cert-abcdef"
    audit_comment = "initial config"
  }
]
```
Full code for the above example can be found [here](https://github.com/PaloAltoNetworks/terraform-provider-cloudngfwaws/tree/main/examples/modules/ezrulestack).

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `source` (String) The module path.
- `rulestack` (Block List, Min: 1, Max: 1) 

### Optional

- `security_rules` (Set of Block List) Set of Security Rules.
- `feeds` (Set of Block List) Set of intelligent feeds.
- `prefix_lists` (Set of Block List) Set of prefix lists.
- `fqdn_lists` (Set of Block List) Set of fqdn lists.
- `custom_url_categories` (Set of Block List) Set of custom url categories.
- `predefined_url_category_overrides` (Set of Block List) Set of predefined url category overrides.
- `certificates` (Set of Block List) Set of certificates.
- `commit` (Boolean) default is `true`.
